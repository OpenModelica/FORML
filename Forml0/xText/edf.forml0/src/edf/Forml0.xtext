grammar edf.Forml0 with org.eclipse.xtext.common.Terminals
// Absolute minimal FORM-L subset:
//	- Single, flat name space
//	- Only Booleans, Integers, Reals, Events / DTLs, CTLs and Properties
//	- Single combined declaration & definition for each
//	- Parameters other than time only for external variables and events
//	- Forward references allowed
//	- No local items for variables and events
//	- No default value, no derivatives, no integrals
//	- No declaration of visibility 
//	- No sets, no spatial locators
//	- No defined types
//	- No sliding time locators (STL), no time exclusion (otherwise)
//	- No composite statements
//	- No objects, no classes
//	- No contracts, no standard contracts
//	- No refinements, no refinement models
//	- No partial models, no libraries
//	- No Guarantee properties

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate forml0 "http://www.Forml0.edf"

Model:
	items+=Item*;
	
Item:
	boolean=Boolean
|	integer=Integer
|	real=Real
|	event=Event
|	ctl=Ctl
|	property=Property
;
 
Boolean returns Item:
	{Boolean} (constant?='constant' | fixed?='fixed')? type='Boolean' name=ID Parameters? booleanDefinition=BooleanDefinition ';'
	/// Verify name is unique
	/// Verify, if there are parameters, that definition is external
	/// Verify, if constant or fixed, that definition is global, and that the value given is itself constant or fixed
	/// Variability 
	///	- If declared constant or fixed, then as declared 
	///	- If variability not declared and global value definition, then as global value
	///	- Else, normal
;

Integer returns Item:
	{Integer} (constant?='constant' | fixed?='fixed')? type='Integer' name=ID Parameters? integerDefinition=IntegerDefinition ';'
	/// Verify name is unique
	/// Verify, if there are parameters, that definition is external
	/// Verify, if constant or fixed, that definition is global, and that the value given is itself constant or fixed
	/// Variability: same rule as for Boolean
;

Real returns Item:
	{Real} (constant?='constant' | fixed?='fixed')? type='Real' name=ID Parameters? realDefinition=RealDefinition ';'
	/// Verify name is unique
	/// Verify, if there are parameters, that definition is external
	/// Verify, if constant or fixed, that definition is global, and that the value given is itself constant or fixed
	/// Variability: same rule as for Boolean
;

Event returns Item:
	{Event} type='Event' name=ID Parameters? eventDefinition=EventDefinition ';'
	/// Verify name is unique
	/// Verify, if there are parameters, that definition is external
;

fragment Parameters *:
	'(' parameters+=Parameter? (',' parameters+=Parameter?)* ')'
; 

Parameter:
	(constant?='constant' | fixed?='fixed')? type=('Boolean' | 'Integer' | 'Real' | 'Event')
;

BooleanDefinition:
	global?='is' (globalValue=BooleanExpression | external?='external')
|	{BooleanDefinition} 
		'begin' 
		(clock?='clock' 'is' clockValue=EventExpression ';')? 
		statements+=BooleanStatement* 
		'end'
;

BooleanStatement: {BooleanStatement} 
	'define'? BooleanAssignment ';'
|	'define' BooleanEvent '.' Rate ';'
	/// Verify, if Rate is discrete, that container has a clock
|	ctl=CtlExpression 'define' (
		BooleanAssignment
	|	BooleanEvent '.' Rate
		/// Verify, if Rate is discrete, that container has a clock
	) ';'
|	dtl=DtlExpression 'define' (
		BooleanAssignment
	|	BooleanEvent '.' DiscreteRate	
	) ';'
;

fragment BooleanAssignment *:
	attribute=AssignableVariableAttribute 'is' value=BooleanExpression
;

fragment BooleanEvent *:
	'(' 'value' becomes?='becomes' target=('true' | 'false') ')'
|	'(' 'value' changes?='changes' ')'
;

IntegerDefinition:
	global?='is' (globalValue=IntegerExpression | external?='external')
|	{IntegerDefinition} 
		'begin' 
		(clock?='clock' 'is' clockValue=EventExpression ';')? 
		statements+=IntegerStatement* 
		'end'
;

IntegerStatement: {IntegerStatement} 
	'define'? IntegerAssignment ';'
	/// Verify value is scalar
|	'define' NumericEvent '.' Rate ';'
	/// Verify, if Rate is discrete, that container has a clock
|	ctl=CtlExpression 'define' (
		IntegerAssignment	
		// Verify value is scalar
	|	NumericEvent '.' Rate
		/// Verify, if Rate is discrete, that container has a clock
	) ';'	
|	dtl=DtlExpression 'define' (
		IntegerAssignment
		/// Verify value is scalar
	|	NumericEvent '.' DiscreteRate
	) ';'
;

fragment IntegerAssignment *:
	attribute=AssignableVariableAttribute 'is' value=IntegerExpression
;

fragment NumericEvent *:
	'(' 'value' becomes?='becomes' target=Domain ')'
|	'(' 'value' leaves?= 'leaves'  origin=Domain (becomes?='becomes' target=Domain)? ')'
|	'(' 'value' changes?='changes' ')'
;

RealDefinition:
	global?='is' (globalValue=NumericExpression | external?='external')
|	{RealDefinition} 
		'begin' 
		(clock?='clock' 'is' clockValue=EventExpression ';')? 
		statements+=NumericStatement* 
		'end'
;

NumericStatement: {NumericStatement} 
	'define'? (
		NumericAssignment
		/// Verify value is scalar
	|	DerivativeAssignment
		/// Verify value has quantity 1/(time*rank)
	) ';'
|	'define' NumericEvent '.' Rate ';'
	/// Verify, if Rate is discrete, that container has a clock
|	ctl=CtlExpression 'define' (
		NumericAssignment
		/// Verify value is scalar
	|	DerivativeAssignment
		/// Verify value has quantity 1/(time*rank)
	|	NumericEvent '.' Rate
		/// Verify, if Rate is discrete, that container has a clock
	) ';'
|	dtl=DtlExpression 'define' (
		NumericAssignment
		/// Verify value is scalar
	|	DerivativeAssignment
		/// Verify value has quantity 1/(time*rank)
	|	NumericEvent '.' DiscreteRate
	) ';'
;

fragment NumericAssignment *:
	attribute=AssignableVariableAttribute 'is' value=NumericExpression
;

fragment DerivativeAssignment *:
	derivative?='derivative' => (ranked?='(' rank=INT ')')? 'is' value=NumericExpression
;

AssignableVariableAttribute: 
	'value'
|	'next'
;

EventDefinition:
	global?='is' (globalValue=EventExpression | external?='external')
|	{EventDefinition} 
		'begin' 
		(clock?='clock' 'is' clockValue=EventExpression ';')? 
		statements+=EventStatement*
		'end'
;

EventStatement:
	'define'? 				   (EventAssignment | Rate) ';' 
	/// Verify, if Rate is discrete, that container has a clock
|	ctl=CtlExpression 'define' (EventAssignment | Rate) ';' 
	/// Verify, if Rate is discrete, that container has a clock
|	dtl=DtlExpression 'define' (EventAssignment | DiscreteRate | signal?='signal' 'occurrence') ';' 
;

fragment EventAssignment *:
	occurrence?='occurrence' 'is' value=EventExpression
;

fragment Rate *:
	rate?='rate' 			 'is' rateValue=RateExpression
|	discrete?='discreteRate' 'is' discreteRateValue=DiscreteRateExpression
;

fragment DiscreteRate *:
	discrete?='discreteRate' 'is' discreteRateValue=DiscreteRateExpression
;

Property returns Item:
	{Property} type=PropertyType name=ID propertyDefinition=PropertyDefinition ';'
	/// Verify name is unique
	/// Verify, if constraintType is 'achieve', that
	///	constraint is a comparison between a monotonic expression and a constant or fixed expression, or two opposite monotonic
;

PropertyDefinition:
	'is' (timeLocator=TimeLocator)? ConstraintType constraint=BooleanExpression
|	{PropertyDefinition}	
		block?='begin'
		(clock?='clock' 'is' clockValue=EventExpression ';')? 
		(timeLocator=TimeLocator)? ConstraintType constraint=BooleanExpression ';'
		'end'
;

PropertyType:
	'Property'
|	'Objective'
|	'Assumption'
|	'Requirement'
|	'Guard'
;

fragment ConstraintType *:
	(can?='can')? (achieve?='achieve' (atClosing?='at' 'closing')? | ensure?='ensure')
;

Ctl returns Item: 
	{Ctl} type='Ctl' name=ID ctlDefinition=CtlDefinition ';'
	/// Verify name is unique
;


CtlDefinition:
	'is' ctl=CtlExpression
|	{CtlDefinition}	
		block?='begin'
		(clock?='clock' 'is' clockValue=EventExpression ';')? 
		ctl=CtlExpression ';'
		'end'
;

TimeLocator:
	dtl=DtlExpression 
|	ctl=CtlExpression
;


DtlExpression:
	when?='when'? value=EventExpression
	// To be improved
;

CtlExpression:
	FlatCtl (
		{CtlEscapeFrom .left=current} 'escape' 'from'    right=EventExpression 
	|	{CtlEscapeAfter.left=current} 'escape' 'after'   right=EventExpression 
	|	{CtlExitFrom   .left=current} 'exit'   'from'    right=EventExpression 
	|	{CtlExitAfter  .left=current} 'exit'   'after'   right=EventExpression 
	)?
;
 
FlatCtl returns CtlExpression:
	{FlatCtl} 'flat' ctl=OrCtl
|	OrCtl
;


OrCtl returns CtlExpression:
	NotCtl ({OrCtl.left=current} 'also' right=NotCtl)*
;

NotCtl returns CtlExpression:
	{NotCtl} 'except' ctl=ModifiedCtl
|	ModifiedCtl
;

ModifiedCtl returns CtlExpression:
	BasicCtl 
	(	{CtlDelayed    .left=current} 'delayed'         right=DurationExpression
	|	{CtlExceptBool .left=current} 'except' 'during' right=BooleanExpression
	|	{CtlExceptCtl  .left=current} 'except' '('      right=CtlExpression      ')'
	|	{CtlExceptEvent.left=current} 'except' 'when'   right=EventExpression
	|	{CtlExceptFirst.left=current} 'except' 'first'  right=DurationExpression
	|	{CtlDuring     .left=current} 'during'          right=BooleanExpression
	)*
;

BasicCtl returns CtlExpression:
	{Always}  'always'
|	{During}  'during' (condition=Expression)
	/// Verify expression is a Boolean or a Ctl
|	{From}    'from'   bop=EventExpression (('until' | before?='before') eop=EventExpression | ('for' | within?='within') duration=DurationExpression)?
|	{After}   'after'  bop=EventExpression (('until' | before?='before') eop=EventExpression | ('for' | within?='within') duration=DurationExpression)?  
|	{Before}  'before' eop=EventExpression
|	{Until}   'until'  eop=EventExpression  
|	{For}     'for'    duration=DurationExpression 
|	{Within}  'within' duration=DurationExpression 

|	{FromEvery}     'from'  'every' duration1=DurationExpression ('for' | within?='within') duration2=DurationExpression 
|	{AfterEvery}    'after' 'every' duration1=DurationExpression ('for' | within?='within') duration2=DurationExpression 
;

Domain: 
	value=VariableExpression
|	interval=Interval
|	'{' intervals+=ValueOrInterval (',' intervals+=ValueOrInterval)* '}'
;

ValueOrInterval:
	value=VariableExpression
|	interval=Interval
; 

Interval:
	{NumericInterval} (bb?='[' | ']') (lb=NumericExpression)? ',' (ub=NumericExpression)? ('[' | eb?=']')
|	{IntegerInterval} (negativeLb?='-' intLb=INT)? '..' (negativeUb?='-' intUb=INT)?;

DurationExpression:
	expression=IfExpression
	/// Verify type is numeric
	/// Verify quantity is time or tick
	/// Verify, if tick, that type is integer
	/// Verify, if tick, that item being defined has a clock
;

RateExpression:
	expression=IfExpression
	// Verify type is numeric
	// Verify quantity is 1/time
;

DiscreteRateExpression:
	expression=IfExpression
	/// Verify type is numeric
	/// Verify quantity is 1/tick
;

BooleanExpression:
	expression=IfExpression
	/// Verify expression is a Boolean
;

IntegerExpression:
	expression=IfExpression
	/// Verify expression is an integer
;

NumericExpression:
	expression=IfExpression	
	/// Verify expression is numeric
;

EventExpression:
	expression=Expression
	/// Verify expression is an event
;

VariableExpression: 
	IfExpression
;

Expression:
	/// An expression can be a Boolean, a number or an event
	/// Not all expression operators are allowed for certain of these types
 	IfExpression  
	(	{ChangesExpression.expression=current} 'changes'
	/// Verify expression is a Boolean or a numeric
	/// Type is event
	|	{BecomesExpression.expression=current} 'becomes' target=Domain 
	/// Verify expression is a Boolean or a numeric
	/// Type is event
	|	{LeavesExpression .expression=current} 'leaves'  origin=Domain (becomes?='becomes' target=Domain)?
	/// Verify expression is a Boolean or a numeric
	/// Type is event
	)?
|	{EveryExpression} 'at' 'every' duration=DurationExpression
	// AT added to overcome parsing problems
	/// Type is event
;

IfExpression returns Expression:
	XorExpression
|	{IfExpression} 'if' condition=BooleanExpression 'then' then=XorExpression 'else' else=IfExpression
	// The else part is not optional, since otherwise there would be no value for the expression
	/// Verify then and else are both Boolean, both events, or both numeric
	// Verify, if both are numeric, that they are of the same quantity
	/// Type is integer if both then and else are 
	/// Type is real if both then and else are numeric and one is real
	/// Type is then's type if it is Boolean or event
	/// Variability
	///	- If all three are constant, then constant
	///	- If condition if fixed, and both branches are constant or fixed, then fixed
	///	- Else normal
	// Quantity is then's quantity
;

XorExpression returns Expression:
	OrExpression (
		{XorExpression.left=current} 'xor' right=OrExpression
		/// Verify both left and right are Booleans, or both are events
		/// Type is left's type
		/// Variability
		///	- If both are constant, then constant
		///	- If both are fixed, or one is fixed and the other constant, then fixed
		///	- Else normal
	)?
;

OrExpression returns Expression:
	AndExpression (
		{OrExpression.left=current} 'or' right=AndExpression
		/// Verify both left and right are Booleans, or both are events
		/// Type is left's type
		/// Variability
		///	- If both are constant, then constant
		///	- If both are fixed, or one is fixed and the other constant, then fixed
		///	- Else normal
	)*
;

AndExpression returns Expression:
	EquationExpression (
		{AndExpression.left=current} 'and' right=EquationExpression
		/// Verify both are Booleans
		/// Type is Boolean
		/// Variability
		///	- If both are constant, then constant
		///	- If both are fixed, or one is fixed and the other constant, then fixed
		///	- Else normal
	|	{WhileExpression.left=current} 'while' right=EquationExpression
		/// Verify left is an event and right is an event or a Boolean
		/// Type is event
	)*
;

EquationExpression returns Expression:
	OrderingExpression (
		/// Variability for all
		///	- If both are constant, then constant
		///	- If both are fixed, or one is fixed and the other constant, then fixed
		///	- Else normal
		/// Type for all is Boolean
		/// Verify for all that both left and right are Booleans, or that both are numeric
		/// Verify for all, if numeric, that left and right are of the same quantity
		{EqualityExpression    .left=current} '='  right=OrderingExpression
	|	{DifferenceExpression  .left=current} '<>' right=OrderingExpression
	)?
;

OrderingExpression returns Expression:
	AdditionExpression (
		/// Variability for all
		///	- If both are constant, then constant
		///	- If both are fixed, or one is fixed and the other constant, then fixed
		///	- Else normal
		/// Type for all is Boolean
		// Verify for all that both left and right are numeric and of the same quantity
		{LessThanExpression      .left=current} '<'  right=AdditionExpression
	|	{LessOrEqualExpression   .left=current} '<=' right=AdditionExpression
	|	{GreaterThanExpression   .left=current} '>'  right=AdditionExpression
	|	{GreaterOrEqualExpression.left=current} '>=' right=AdditionExpression
	)?
;

AdditionExpression returns Expression:
	MinusExpression (
		{AdditionExpression.left=current} '+' right=MinusExpression
		/// Verify both are numeric, or one is an event and the other is a duration
		/// Verify, if both are numeric, that they are of the same quantity
		/// Type is integer if left and right are integers
		/// Type is real if left and right are numeric, and one is a real 
		/// Type is event if one is an event and the other is numeric
		/// Variability
		///	- If both are constant, then constant
		///	- If both are fixed, or one is fixed and the other constant, then fixed
		///	- If one is increasing and the other constant, fixed or increasing, then increasing
		///	- If one is decreasing and the other constant, fixed or decreasing, then decreasing
		///	- Else normal
		/// Quantity is left's quantity if left and right are numeric, none if one is an event
	)*
;

MinusExpression returns Expression:
	ProductExpression (
		{SubstractionExpression.left=current} '-' right=ProductExpression
		// Verify both left and right are numeric and of the same quantity
		/// Type is integer if left and right are integers, else type is real
		/// Variability
		///	- If both are constant, then constant
		///	- If both are fixed, or one is fixed and the other constant, then fixed
		///	- If left is increasing and right is constant, fixed or decreasing, then increasing
		///	- If left is decreasing and right is constant, fixed or increasing, then increasing
		///	- Else normal
		/// Quantity is left's quantity
	|	{WithoutExpression.left=current} 'without' right=ProductExpression
		/// Verify both left and right are events
		/// Type is event
	|	{FollowingExpression.left=current} 'following' right=ProductExpression
		/// Verify both left and right are events
		/// Type is event
	)?
;

ProductExpression returns Expression:
	DivisionExpression (
		{ProductExpression.left=current} '*' right=DivisionExpression
		/// Verify both left and right are numeric
		/// Result is an integer if left and right are integers, else it is a real
		/// Variability
		///	- If both are constant, then constant
		///	- If both are fixed, or one is fixed and the other constant, then fixed
		///	- If one is a positive constant and the other increasing, then increasing
		///	- If one is a negative constant and the other increasing, then decreasing
		///	- If one is a positive constant and the other decreasing, then decreasing
		///	- If one is a negative constant and the other decreasing, then increasing
		///	- Else normal
		/// Quantity is left's quantity + right's quantity
	)*
;

DivisionExpression returns Expression:
	FirstOrDropExpression (
		{DivisionExpression.left=current} '/' right=FirstOrDropExpression
		/// Verify both left and right are numeric
		/// Verify, if right is constant, that it is not zero
		/// Result is a real
		/// Variability: same rules as for ProductExpression
		/// Quantity is left's quantity - right's quantity
	|	{IntegerDivisionExpression.left=current} '\\' right=FirstOrDropExpression
		/// Verify both left and right are numeric
		/// Verify, if right is constant, that it is not zero
		/// Result is an integer
		/// Variability: same rules as for ProductExpression
		/// Quantity is left's quantity - right's quantity
	)?
;

FirstOrDropExpression returns Expression:
	{FirstExpression} 'first' => (n?='(' left=INT ')')? right=FirstOrDropExpression
	/// Verify right is an event
	/// Type is event
|	{DropExpression} 'drop' 'first' => (n?='(' left=INT ')')? right=FirstOrDropExpression
	/// Verify right is an event
	/// Type is event
|	NegationExpression
;

NegationExpression returns Expression:
	{UnaryMinusExpression} =>'-' right=PowerExpression
	/// Verify right is numeric
	/// Type is right's type 
	/// Variability
	///	- If right variability is constant, fixed or normal, then right variability
	///	- If right variability is increasing, then decreasing
	///	- If right variability is decreasing, then increasing
	/// Quantity is right's quantity
|	{NotExpression} =>'not' right=PowerExpression
	/// Verify right is a Boolean
	/// Type is Boolean
|	PowerExpression
;

PowerExpression returns Expression:
	AttributeExpression (
		{PowerExpression.left=current} '^' (negative?='-')? right=INT
		/// Verify left is numeric
		/// Verify, if left is constant and negative is true, that left is not zero
		/// Type is left's type
		/// Variability 
		///	- If left's variability is constant or fixed, then left's variability
		///	- Else if not negative, then left's variability 
		///	- Else if negative, variability is normal
		/// Quantity is left's quantity * right, or * - right if negative
	)*	
;

AttributeExpression returns Expression:
	Atom (
		{AttributeExpression.atom=current} ('.' (
			rate?='rate' 
			///	Verify atom is of type event	Variability			Value
			///	- EventLiteral					normal				unknown
			///	- PropertyEvent					normal				unknown
			///	- MyClock						atom's				atom's
			// 		Verify container has a clock
			///	- Clock							atom's				atom's
			// 		Verify reference is an event
			///	- Reference						atom's				atom's
			// 		Verify atom has a clock
			///	- parenthesized Expression		atom's				atom's
			// 		Verify Expression is of type event
			/// Type is real
			///	Quantity is 1/time
		|	previous?='previous' 
			///	Verify atom is a variable
			/// Type is atom's type
			///	Variability is atom's variability
			///	Quantity is atom's quantity
			///	Value is atom's value
		|	derivative?='derivative' => (ranked?='(' rank=INT ')')?
			/// Verify atom is a Real
			///	Type is numeric
			/// Variability is constant if atom is constant or fixed, normal otherwise
			///	Quantity is atom's quantity/time*rank
			///	Value is 0 if atom is constant or fixed, unknown otherwise
		|	integral?='integral' => (ranked?='(' rank=INT ')')?
			/// Verify atom is a Real
			///	Type is numeric
			///	Variability is constant if atom is constant 0, normal otherwise
			///	Quantity is atom's quantity*time*rank
			///	Value is 0 if atom is constant 0, unknown otherwise
		))?
	)
;

Atom returns Expression:
	/// Numeric (integer or real) atoms
	{NumericLiteral} value=NumericLiteral
	/// Type is integer or real
	/// Variability is constant
	/// Quantity is none
	/// Value is literal's value
|	{Second} unit='s'
	/// Type is integer
	/// Variability is constant
	/// Quantity is time
	/// Value is 1
|	{Time} time='time'
	/// Type is real
	/// Variability is increasing
	/// Quantity is time
|	{inPTime} time='inPTime'
	/// Type is real
	/// Variability is increasing
	/// Quantity is time
|	{Tick} unit='tick'
	/// Verify containing item has a clock
	/// Type is integer
	/// Variability is constant
	/// Quantity is tick
	/// Value is 1
|	{ClockTime} time='clockTime'
	/// Verify containing item has a clock
	/// Type is integer
	/// Variability is increasing
	/// Quantity is tick
|	{InPClockTime} time='inPClockTime'
	/// Verify containing item has a clock
	/// Type is integer
	/// Variability is increasing
	/// Quantity is tick
|	{MyDerivative} (derivative?='derivative' | integral?='integral') => (ranked?='(' rank=INT ')')?
	/// Verify containing item is a Real
	/// Type is numeric
	/// If derivative, variability is constant if containing item is constant or fixed, normal otherwise
	/// If integral, variability is constant if containing item is constant 0, normal otherwise
	/// If derivative, value is 0 if containing item is constant or fixed, unknown otherwise 
	/// If integral, value is 0 if containing item is constant 0, unknown otherwise
	/// If derivative, quantity is 1/time*rank
	/// If integral, quantity is 1*time*rank
|	{MyRate} rate='rate'
	/// Verify currently defined item is an event
	/// Verify item's rate is defined globally 
	/// Type is real
	/// Variability is item's rate variability
	/// Quantity is rate 
	/// Value, if constant, is item's rate value
|	{PropertyPfd} identifier=[Property] '.' 'pfd'
	/// Type is real
	/// Variability is normal
	/// Quantity is none
|	{BuiltInFunctionCall} function=BuiltInFunction '(' argument=Expression ')'
	/// Verify, if tick function, that containing item has a clock
	/// Verify argument's type: see BuiltInFunction
	/// Type:      				see BuiltInFunction
	/// Variability: 			see BuiltInFunction
	/// Quantity: 				see BuiltInFunction
	
	/// Boolean atoms
|	{BooleanLiteral} value=('true' | 'false')
	/// Type is Boolean
	/// Variability is constant
|	{PropertyState} identifier=[Property] '.' state=('satisfaction' | 'violation')
	/// Type is Boolean
	/// Variability is normal
|	{BooleanFromCtl} 'boolean' '(' ctl=CtlExpression ')'
	/// Type is Boolean
	/// Variability is normal

	/// Variable (integer, real or Boolean) atoms
|	{MyValue} attribute=('value' | 'previous')
	/// Verify currently defined item is a variable
	/// Type is currently defined item's type
	/// Variability is currently defined item's variability
	/// Quantity is none
	/// Value is currently defined item's value
	
	/// Event atoms
|	{EventLiteral} 't0'
	/// Type is event
|	{PropertyEvent} identifier=[Property] '.' event=('eSatisfaction' | 'eViolation')
	/// Type is event
|	{MyClock} clock='clock'
	/// Verify containing item has a clock
	/// Type is event
|	{Clock} identifier=[Item] '.' 'clock'
	/// Type is event

	/// Variable or event atoms
|	{Reference} identifier=[Item] 
	/// Type is identifier's type
	/// Variability is identifier's variability
	/// Quantity is none
	/// Value is identifier's value
|	{FunctionCall} function=[Item] '(' arguments+=Expression (',' arguments+=Expression)* ')'
	/// Verify function is a variable or an event
	/// Verify number of arguments is equal to number of function parameters
	/// Verify each argument type and variability match the specified function parameter type and variability
	/// Type is function's type
	/// Variability is normal
	/// Quantity is none
|	'(' Expression ')' 
;

BuiltInFunction:
						// Argument's type			Type				Variability		Quantity
	'count'				// (Event)				==>	Integer				increasing		none
|	'duration'			// (Boolean)			==>	Real				increasing		time
|	'clockDuration'		// (Boolean)			==>	Integer				increasing		tick
|	'inPCount'			// (Event)				==>	Integer				increasing		none
|	'inPDuration'		// (Boolean)			==>	Real				increasing		time
|	'inPClockDuration'	// (Boolean)			==>	Integer				increasing		tick
|	'inPMax'			// (Integer or Real)	==>	Integer or Real		increasing		argument's
|	'inPMin'			// (Integer or Real)	==>	Integer or Real		decreasing		argument's
|	'inTMax'			// (Integer or Real)	==>	Integer or Real		increasing		argument's
|	'inTMin'			// (Integer or Real)	==>	Integer or Real		decreasing		argument's
|	'probability'		// (Event)				==>	Real				normal			none
;
 
NumericLiteral:
	((integerValue=INT (=> decimalValue=DECIMALINT | decimalPoint?='.')?) | decimalValue=DECIMALINT) => (exponent=EXPONENT)?
	// This definition needs to be improved:
	//	- White spaces are allowed between different parts of the literal, but they should not
	//	- A + or - sign is required after letter e or E
;

terminal DECIMALINT returns ecore::EString:  '.' ('0'..'9')+;
terminal EXPONENT   returns ecore::EString:	 ('e' | 'E') ('+' | '-') INT;


@Override
terminal ID: ('a'..'z'|'A'..'Z'|'_'|'°') ('a'..'z'|'A'..'Z'|'_'|'°'|'0'..'9')*;
