grammar edf.Forml0 with org.eclipse.xtext.common.Terminals
// Absolute minimal FORM-L subset:
//	- Single, flat name space
//	- Only Booleans, Integers, Reals, Events / DTLs, CTLs and Properties
//	- Single combined declaration & definition for each
//	- Parameters other than time only for external variables and events
//	- Forward references allowed
//	- No local items for variables and events
//	- No default value, no derivatives, no integrals
//	- No declaration of visibility 
//	- No sets, no spatial locators
//	- No defined types
//	- No sliding time locators (STL), no time exclusion (otherwise)
//	- No composite statements
//	- No objects, no classes
//	- No contracts, no standard contracts
//	- No refinements, no refinement models
//	- No partial models, no libraries
//	- No Guarantee properties

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate forml0 "http://www.Forml0.edf"

Model:
	items+=Item*;
	
Item:
	boolean=Boolean
|	integer=Integer
|	real=Real
|	event=Event
|	ctl=Ctl
|	property=Property
;
 
Boolean returns Item:
	{Boolean} (constant?='constant' | fixed?='fixed')? type='Boolean' name=ID Parameters? booleanDefinition=BooleanDefinition ';'
	/// Verify name is unique
	/// Verify, if there are parameters, that definition is external
	/// Verify, if constant or fixed, that definition is global, and that the value given is itself constant or fixed
	/// Variability 
	///	- If declared constant or fixed, then as declared 
	///	- If variability not declared and global value definition, then as global value
	///	- Else, normal
;

Integer returns Item:
	{Integer} (constant?='constant' | fixed?='fixed')? type='Integer' name=ID Parameters? integerDefinition=IntegerDefinition ';'
	/// Verify name is unique
	/// Verify, if there are parameters, that definition is external
	/// Verify, if constant or fixed, that definition is global, and that the value given is itself constant or fixed
	/// Variability: same rule as for Boolean
;

Real returns Item:
	{Real} (constant?='constant' | fixed?='fixed')? type='Real' name=ID Parameters? realDefinition=RealDefinition ';'
	/// Verify name is unique
	/// Verify, if there are parameters, that definition is external
	/// Verify, if constant or fixed, that definition is global, and that the value given is itself constant or fixed
	/// Variability: same rule as for Boolean
;

Event returns Item:
	{Event} type='Event' name=ID Parameters? eventDefinition=EventDefinition ';'
	/// Verify name is unique
	/// Verify, if there are parameters, that definition is external
;

fragment Parameters *:
	'(' parameters+=Parameter? (',' parameters+=Parameter?)* ')'
; 

Parameter:
	(constant?='constant' | fixed?='fixed')? type=('Boolean' | 'Integer' | 'Real' | 'Event')
;

BooleanDefinition:
	global?='is' (globalValue=BooleanExpression | external?='external')
|	{BooleanDefinition} 
		'begin' 
		(clock?='clock' 'is' clockValue=EventExpression ';')? 
		(value?='value' 'is' globalValue=BooleanExpression ';' | statements+=BooleanStatement+)? 
		'end'
;

BooleanStatement: {BooleanStatement} 
	timeLocator=TimeLocator 'define' attribute=AssignableVariableAttribute 'is' value=BooleanExpression ';'
;

IntegerDefinition:
	global?='is' (globalValue=IntegerExpression | external?='external')
|	{IntegerDefinition} 
		'begin' 
		(clock?='clock' 'is' clockValue=EventExpression ';')? 
		(value?='value' 'is' globalValue=IntegerExpression ';' | statements+=IntegerStatement+)? 
		'end'
;

IntegerStatement: {IntegerStatement} 
	timeLocator=TimeLocator 'define' attribute=AssignableVariableAttribute 'is' value=IntegerExpression ';'
;

RealDefinition:
	global?='is' (globalValue=NumericExpression | external?='external')
|	{RealDefinition} 
		'begin' 
		(clock?='clock' 'is' clockValue=EventExpression ';')? 
		(value?='value' 'is' globalValue=NumericExpression ';' | statements+=NumericStatement+)? 
		'end'
;

NumericStatement: {NumericStatement} 
	timeLocator=TimeLocator 'define' attribute=AssignableVariableAttribute 'is' value=NumericExpression ';'
;

AssignableVariableAttribute: 
	'value'
|	'next'
;

EventDefinition:
	global?='is' (globalValue=EventExpression | external?='external')
|	{EventDefinition} 
		'begin' 
		(clock?='clock' 'is' clockValue=EventExpression ';')? 
		(	occurrence?='occurrence' 'is' globalValue=EventExpression ';' 
		|	rate?='rate'       		 'is' rateValue=NumericExpression ';'
		/// Verify rateValue is constant
		|	statements+=EventStatement+
		)? 
		'end'
;

EventStatement:
	ctl=CtlExpression   'define' ('occurrence' 'is' occurrence=EventExpression | 'rate' 'is' rate=NumericLiteral)';' 
|	dtl=DtlExpression	('define' 'occurrence' 'is' occurrence=EventExpression | 'signal' 'occurrence')                              ';'
;

Property returns Item:
	{Property} type=PropertyType name=ID propertyDefinition=PropertyDefinition ';'
	//	{Property} type=PropertyType name=ID 'is' (timeLocator=TimeLocator)? ConstraintType constraint=BooleanExpression ';'
	/// Verify name is unique
	/// Verify, if constraintType is 'achieve', that
	///	constraint is a comparison between a monotonic expression and a constant or fixed expression, or two opposite monotonic
;

PropertyDefinition:
	'is' (timeLocator=TimeLocator)? ConstraintType constraint=BooleanExpression
|	{PropertyDefinition}	
		'begin'
		(clock?='clock' 'is' clockValue=EventExpression ';')? 
		(timeLocator=TimeLocator)? ConstraintType constraint=BooleanExpression
		'end'
;

PropertyType:
	'Property'
|	'Objective'
|	'Assumption'
|	'Requirement'
|	'Guard'
;

fragment ConstraintType *:
	(can?='can')? (achieve?='achieve' (atClosing?='at' 'closing')? | ensure?='ensure')
;

Ctl returns Item: 
	{Ctl} type='Ctl' name=ID 'is' value=CtlExpression ';'
	/// Verify name is unique
;
	
TimeLocator:
	DtlExpression 
|	CtlExpression
;


DtlExpression:
	'when'? EventExpression
	// To be improved
;

CtlExpression:
	FlatCtl (
		{CtlEscapeFrom .left=current} 'escape' 'from'    right=EventExpression 
	|	{CtlEscapeAfter.left=current} 'escape' 'after'   right=EventExpression 
	|	{CtlExitFrom   .left=current} 'exit'   'from'    right=EventExpression 
	|	{CtlExitAfter  .left=current} 'exit'   'after'   right=EventExpression 
	)?
;
 
FlatCtl returns CtlExpression:
	{FlatCtl} 'flat' ctl=OrCtl
|	OrCtl
;


OrCtl returns CtlExpression:
	NotCtl ({OrCtl.left=current} 'also' right=NotCtl)*
;

NotCtl returns CtlExpression:
	{NotCtl} 'except' ctl=ModifiedCtl
|	ModifiedCtl
;

ModifiedCtl returns CtlExpression:
	BasicCtl 
	(	{CtlDelayed    .left=current} 'delayed'         right=Duration
	|	{CtlExceptBool .left=current} 'except' 'during' right=BooleanExpression
	|	{CtlExceptEvent.left=current} 'except' 'when'   right=EventExpression
	|	{CtlExceptFirst.left=current} 'except' 'first'  right=Duration
	|	{CtlDuring     .left=current} 'during'          right=BooleanExpression
	)*
;

BasicCtl returns CtlExpression:
	{Always}  'always'
|	{During}  'during' (condition=Expression)
	/// Verify expression is a Boolean or a Ctl
|	{From}    'from'   bop=EventExpression (('until' | 'before') eop=EventExpression | ('for' | 'within') duration=Duration)?
|	{After}   'after'  bop=EventExpression (('until' | 'before') eop=EventExpression | ('for' | 'within') duration=Duration)?  
|	{Before}  'before' eop=EventExpression
|	{Until}   'until'  eop=EventExpression  
|	{For}     'for'    duration=Duration 
|	{Within}  'within' duration=Duration 

|	{FromEveryFor}     'from'  'every' duration1=Duration ('for' | 'within') duration2=Duration 
|	{AfterEveryFor}    'after' 'every' duration1=Duration ('for' | 'within') duration2=Duration 
;

Duration:
	value=NumericExpression (ticks?='ticks')?
	/// Verify value > 0 if value is constant
	/// Verify value is an integer if ticks is present
	/// Verify containing item has a clock if ticks is present
;

Domain: 
 	intervals+=ValueOrInterval
|	'{' intervals+=ValueOrInterval (',' intervals+=ValueOrInterval)* '}'
;

ValueOrInterval:
	value=VariableExpression
|	interval=Interval
; 

Interval:
	{Interval} (bb?='[' | ']') (lb=NumericExpression)? ',' (ub=NumericExpression)? ('[' | eb?=']')
|	{IntegerInterval} (negativeLb?='-' intLb=INT)? '..' (negativeUb?='-' intUb=INT)?;

BooleanExpression:
	expression=IfExpression
	/// Verify expression is a Boolean
;

IntegerExpression:
	expression=IfExpression
	/// Verify expression is an integer
;

NumericExpression:
	expression=IfExpression	
	/// Verify expression is numeric
;

EventExpression:
	expression=Expression
	/// Verify expression is an event
;

VariableExpression: 
	IfExpression
;

Expression:
	/// An expression can be a Boolean, a number or an event
	/// Not all expression operators are allowed for certain of these types
 	IfExpression  
	(	{ChangesExpression.expression=current} 'changes'
	/// Verify expression is a Boolean or a numeric
	/// Result is an event
	|	{BecomesExpression.expression=current} 'becomes' target=Domain 
	/// Verify expression is a Boolean or a numeric
	/// Result is an event
	|	{LeavesExpression .expression=current} 'leaves'  origin=Domain ('becomes' target=Domain)?
	/// Verify expression is a Boolean or a numeric
	/// Result is an event
	)?
|	{EveryExpression} 'at' 'every' duration=Duration
	// AT added to overcome parsing problems
	/// Result is an event
;

IfExpression returns Expression:
	XorExpression
|	{IfExpression} 'if' condition=BooleanExpression 'then' then=XorExpression 'else' else=IfExpression
	// The else part is not optional, since otherwise there would be no value for the expression
	/// Verify then and else are both Boolean, both events, or both numeric
	/// Result is an integer if then and else are integers
	/// Result is a real if then and else are numeric and one is a real
	/// Result is of the same type as then if it is a Boolean or an event
	/// Variability
	///	- If all three are constant, then constant
	///	- If condition if fixed, and both branches are constant or fixed, then fixed
	///	- Else normal
;

XorExpression returns Expression:
	OrExpression (
		{XorExpression.left=current} 'xor' right=OrExpression
		/// Verify both left and right are Booleans, or both are events
		/// Result is of the same type as left
		/// Variability
		///	- If both are constant, then constant
		///	- If both are fixed, or one is fixed and the other constant, then fixed
		///	- Else normal
	)?
;

OrExpression returns Expression:
	AndExpression (
		{OrExpression.left=current} 'or' right=AndExpression
		/// Verify both left and right are Booleans, or both are events
		/// Result is of the same type as left
		/// Variability
		///	- If both are constant, then constant
		///	- If both are fixed, or one is fixed and the other constant, then fixed
		///	- Else normal
	)*
;

AndExpression returns Expression:
	EquationExpression (
		{AndExpression.left=current} 'and' right=EquationExpression
		/// Verify both are Booleans
		/// Result is a Boolean
		/// Variability
		///	- If both are constant, then constant
		///	- If both are fixed, or one is fixed and the other constant, then fixed
		///	- Else normal
	|	{WhileExpression.left=current} 'while' right=EquationExpression
		/// Verify left is an event and right is an event or a Boolean
		/// Result is an event
	)*
;

EquationExpression returns Expression:
	OrderingExpression (
		/// Variability for all
		///	- If both are constant, then constant
		///	- If both are fixed, or one is fixed and the other constant, then fixed
		///	- Else normal
		{EqualityExpression    .left=current} '='  right=OrderingExpression
		/// Verify both left and right are Booleans, or both are numeric
		/// Result is a Boolean
	|	{DifferenceExpression  .left=current} '<>' right=OrderingExpression
		/// Verify both left and right are Booleans, or both are numeric
		/// Result is a Boolean
	)?
;

OrderingExpression returns Expression:
	MinusExpression (
		/// Variability for all
		///	- If both are constant, then constant
		///	- If both are fixed, or one is fixed and the other constant, then fixed
		///	- Else normal
		{LessThanExpression    .left=current} '<'  right=MinusExpression
		/// Verify both left and right are numeric
		/// Result is a Boolean
	|	{LessOrEqualExpression   .left=current} '<=' right=MinusExpression
		/// Verify both left and right are numeric
		/// Result is a Boolean
	|	{GreaterThanExpression .left=current} '>'  right=MinusExpression
		/// Verify both left and right are numeric
		/// Result is a Boolean
	|	{GreaterOrEqualExpression.left=current} '>=' right=MinusExpression
		/// Verify both left and right are numeric
		/// Result is a Boolean
	)?
;

MinusExpression returns Expression:
	AdditionExpression (
		{SubstractionExpression.left=current} '-' right=AdditionExpression
		/// Verify both left and right are numeric
		/// Result is an integer if left and right are integers, else it is a real
		/// Variability
		///	- If both are constant, then constant
		///	- If both are fixed, or one is fixed and the other constant, then fixed
		///	- If left is increasing and right is constant, fixed or decreasing, then increasing
		///	- If left is decreasing and right is constant, fixed or increasing, then increasing
		///	- Else normal
	|	{WithoutExpression.left=current} 'without' right=AdditionExpression
		/// Verify both left and right are events
		/// Result is an event
	|	{FollowingExpression.left=current} 'following' right=AdditionExpression
		/// Verify both left and right are events
		/// Result is an event
	)?
;

AdditionExpression returns Expression:
	DivisionExpression (
		{AdditionExpression.left=current} '+' right=DivisionExpression
		/// Verify left is numeric or an event, and right is numeric
		/// Result is an integer if left and right are integers
		/// Result is a real if left and right are numeric, and one is a real 
		/// Result is an event if left is an event and right is numeric (a delay)
		// Variability
		///	- If both are constant, then constant
		///	- If both are fixed, or one is fixed and the other constant, then fixed
		///	- If one is increasing and the other constant, fixed or increasing, then increasing
		///	- If one is decreasing and the other constant, fixed or decreasing, then decreasing
		///	- Else normal
	)*
;

DivisionExpression returns Expression:
	ProductExpression (
		{DivisionExpression.left=current} '/' right=ProductExpression
		/// Verify both left and right are numeric
		/// Verify, if right is constant, that it is not zero
		/// Result is a real
		/// Variability: same rules as for ProductExpression
	|	{IntegerDivisionExpression.left=current} '\\' right=ProductExpression
		/// Verify both left and right are numeric
		// Verify, if right is constant, that it is not zero
		/// Result is an integer
		/// Variability: same rules as for ProductExpression
	)?
;

ProductExpression returns Expression:
	FirstOrDropExpression (
		{ProductExpression.left=current} '*' right=FirstOrDropExpression
		/// Verify both left and right are numeric
		/// Result is an integer if left and right are integers, else it is a real
		/// Variability
		///	- If both are constant, then constant
		///	- If both are fixed, or one is fixed and the other constant, then fixed
		///	- If one is a positive constant and the other increasing, then increasing
		///	- If one is a negative constant and the other increasing, then decreasing
		///	- If one is a positive constant and the other decreasing, then decreasing
		///	- If one is a negative constant and the other decreasing, then increasing
		///	- Else normal
	)*
;

FirstOrDropExpression returns Expression:
	{FirstExpression} 'first' => ('(' left=INT ')')? right=FirstOrDropExpression
	/// Verify right is an event
	/// Result is an event
|	{DropExpression} 'drop' 'first' => ('(' left=INT ')')? right=FirstOrDropExpression
	/// Verify right is an event
	/// Result is an event
|	NegationExpression
;

NegationExpression returns Expression:
	{UnaryMinusExpression} =>'-' right=PowerExpression
	/// Verify right is numeric
	/// Result is of the same type as right
	// Variability
	///	- If right variability is constant, fixed or normal, then right variability
	///	- If right variability is increasing, then decreasing
	///	- If right variability is decreasing, then increasing
|	{NotExpression} =>'not' right=PowerExpression
	/// Verify right is a Boolean
	/// Result is Boolean
|	PowerExpression
;

PowerExpression returns Expression:
	AttributeExpression (
		{PowerExpression.left=current} '^' (negative?='-')? right=INT
		/// Verify left is numeric
		/// Verify, if left is constant and negative is true, that left is not zero
		/// Result is of the same type as left
		/// Variability 
		///	- If left variability is constant or fixed, then left variability
		///	- Else if not negative, then left variability 
		///	- Else if negative, variability is normal
	)*	
;

AttributeExpression returns Expression:
	Atom (
		{AttributeExpression.atom=current} ('.' (rate?='rate' | previous?='previous'))?
		/// If rate
		///	Verify atom is an event
		/// 	Result is a real
		///	Atom could be of class:			Variability			Value
		///		- EventLiteral				constant			0.0
		///		- PropertyEvent				normal
		///		- MyClock					atom's				atom's
		///		- Clock						atom's				atom's
		///		- Reference					atom's				atom's
		///		- parenthesized Expression	atom's				atom's
		/// If previous
		///	Verify atom is a variable
		/// 	Result is atom's result
		///	Variability is atom's variability
		///	Value is atom's value
	)
;

Atom returns Expression:
	/// Numeric (integer or real) atoms
	{NumericLiteral} value=NumericLiteral
	/// Result is an integer or a real
	/// Variability is constant
|	{Time} 'time'
	/// Result is a real
	/// Variability is increasing
|	{inPTime} 'inPTime'
	/// Result is a real
	/// Variability is increasing
|	{MyRate} rate='rate'
	/// Verify currently defined item is an event
	/// Variability 
	///	- If rate is defined globally for currently defined item, then constant
	///	- Else, normal
|	{PropertyPfd} identifier=[Property] '.' 'pfd'
	/// Result is a real
	/// Variability is normal
|	{BuiltInFunctionCall} function=BuiltInFunction '(' argument=Expression ')'
	/// Verify type: see BuiltInFunction
	/// Result:      see BuiltInFunction
	/// Variability: see BuiltInFunction
	
	
	/// Boolean atoms
|	{BooleanLiteral} value=('true' | 'false')
	/// Result is a Boolean
	/// Variability is constant
|	{PropertyState} identifier=[Property] '.' state=('satisfaction' | 'violation')
	/// Result is a Boolean
	/// Variability is normal
|	{BooleanFromCtl} 'boolean' '(' ctl=CtlExpression ')'
	/// Result is a Boolean
	/// Variability is normal

	/// Variable (integer, real or Boolean) atoms
|	{MyValue} attribute=('value' | 'previous')
	/// Verify currently defined item is a variable
	/// Result is of the type of currently defined item
	/// Variability is the same as variability of currently defined item
	
	/// Event atoms
|	{EventLiteral} 't0'
	/// Result is an event
|	{PropertyEvent} identifier=[Property] '.' event=('eSatisfaction' | 'eViolation')
	/// Result is an event
|	{MyClock} clock='clock'
	/// Verify currently defined item is not a Ctl
	/// Result is an event
|	{Clock} identifier=[Item] '.' 'clock'
	/// Verify identifier is not a Ctl
	/// Result is an event

	/// Variable or event atoms
|	{Reference} identifier=[Item] 
	/// Result is of the type of identifier
	// Variability is variability of identifier
|	{FunctionCall} function=[Item] '(' arguments+=Expression (',' arguments+=Expression)* ')'
	/// Verify function is a variable or an event
	/// Verify number of arguments is equal to number of function parameters
	/// Verify each argument type and variability match the specified function parameter type and variability
	/// Result type is function type
	/// Variability is normal
|	'(' Expression ')' 
;

BuiltInFunction:
	'count'			// (Event)				==>	Integer				increasing
|	'duration'		// (Boolean)			==>	Real				increasing
|	'inPCount'		// (Event)				==>	Integer				increasing
|	'inPDuration'	// (Boolean)			==>	Real				increasing
|	'inPMax'		// (Integer or Real)	==>	Integer or Real		increasing
|	'inPMin'		// (Integer or Real)	==>	Integer or Real		decreasing
|	'inTMax'		// (Integer or Real)	==>	Integer or Real		increasing
|	'inTMin'		// (Integer or Real)	==>	Integer or Real		decreasing
|	'probability'	// (Event)				==>	Real				normal
;
 
NumericLiteral:
	((integerValue=INT (=> decimalValue=DECIMALINT | decimalPoint?='.')?) | decimalValue=DECIMALINT) => (exponent=EXPONENT)?
	// This definition needs to be improved:
	//	- White spaces are allowed between different parts of the literal, but they should not
	//	- A + or - sign is required after letter e or E
;

terminal DECIMALINT returns ecore::EString:  '.' ('0'..'9')+;
terminal EXPONENT   returns ecore::EString:	 ('e' | 'E') ('+' | '-') INT;


@Override
terminal ID: ('a'..'z'|'A'..'Z'|'_'|'°') ('a'..'z'|'A'..'Z'|'_'|'°'|'0'..'9')*;
